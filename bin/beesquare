#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use autodie qw(:all);
use WWW::Mechanize;
use Config::Tiny;
use JSON::Any;
use AnyDBM_File;
use POSIX qw(O_CREAT O_RDWR);

use constant DANCE_STUDIO => "First Floor 393";
use constant DEBUG => 0;

my $config = Config::Tiny->read("$ENV{HOME}/.rtbmrc");
my $TOKEN  = "oauth_token=$config->{FourSquare}{auth_token}&v=20121108";
my $BASE   = 'https://api.foursquare.com/v2';

my $mech = WWW::Mechanize->new();
my $json = JSON::Any->new;

my %seen;
tie %seen, 'AnyDBM_File', "$ENV{HOME}/.beesquare.seen", O_CREAT|O_RDWR, 0666;

$mech->get("$BASE/users/self/checkins?$TOKEN");

my $checkins = $json->decode($mech->content)->{response}{checkins}{items};

foreach my $checkin (@$checkins) {
    
    say "$checkin->{id} $checkin->{venue}{name}" if DEBUG;
    
    # Skip entries we've already scored points for
    next if $seen{$checkin->{venue}{id}};

    # TODO: These should all be in a config file, but my tram
    # ride is about to end.
    #
    # TODO: Submit points via WebService::Beeminder
    #
    if ($checkin->{venue}{name} eq DANCE_STUDIO) {
        system('bmndr', 'dance', 1, 'auto: beesquare');
        $seen{$checkin->{venue}{id}} = 1;
    }
}
